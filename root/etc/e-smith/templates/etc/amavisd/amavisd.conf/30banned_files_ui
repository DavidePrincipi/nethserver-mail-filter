{
    #
    # 30banned_files_ui -- add banned file classes and extensions to
    # @banned_namepath_re list
    # 
    if($amavisd{BlockAttachmentStatus} eq 'enabled') {

	#
	# The $regexp* regular expression matches the output of
	# "check_for_banned".  File extension is compared for file
	# name and file type as guessed by file(1).
	#
	# To debug this expression set $log_level=4 and search for
	# "lookup [banned_namepath_re]" in /var/log/messages.
	#

	# Known classes:
	my %extClasses = (
	    'Exec' => [qw(exe exe-ms vb[es]? ws[cfh] ms[cipt] pif scr sct bat cmd com cpl dll jse? inf)],
	    'Arch' => [qw(zip 7z rar tar gz cab bz2?)],
	    );

	if(defined $amavisd{BlockAttachmentClassList}) {
	    foreach $class (split(',', $amavisd{BlockAttachmentClassList})) {
		if(defined $extClasses{$class}) {
		    my $regexpClass = join('|', @{$extClasses{$class}});
		    my $comment = "(?#CLASS $class)";
		    push @banned_namepath_re, qr/$comment ^ (.*\t)? (T=|N=([^\.]+\.)+)($regexpClass) (\t.*)? $/xmi;
		}
	    }
	}

	# Custom list:
	if($amavisd{BlockAttachmentCustomStatus} eq 'enabled' 
	   && defined $amavisd{BlockAttachmentCustomList}) {
	    my @extList = ();
	
	    foreach $extension (split(',', $amavisd{BlockAttachmentCustomList})) {
		# trim whitespace:
		$extension =~ s/\s+//;  
		push @extList, $extension;
	    }

	    if(scalar @extList) {
		my $regexp = join('|', @extList);
	    
		push @banned_namepath_re, qr/(?#CUSTOM) ^ (.*\t)? (T=|N=([^\.]+\.)+)($regexp) (\t.*)? $/xmi;
	    }

	}

    }

    '';
}
